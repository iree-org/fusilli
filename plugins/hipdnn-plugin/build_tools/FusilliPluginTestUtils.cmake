# Copyright 2025 Advanced Micro Devices, Inc.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions.
# See https://llvm.org/LICENSE.txt for license information.
# SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

#===------------------------------------------------------------------------===#
#
# This file contains test utilities for Fusilli's hipDNN plugin.
#
# TheRock CI generates test, lib, dbg, etc. artifacts from the build - CI tests
# are run on separate runners using a downloaded version of a project's test
# artifacts. This approach allows the build to use a cheaper CPU-only machine,
# and the tests for each project to run in parallel on more expensive GPU
# machines (after the build has completed).
#
# In TheRock the pattern is to install tests as well as a generated CTest script
# in the test artifact. When the artifact is downloaded to an isolated runner,
# CTest can simply be pointed at the generated script + pre-built tests.
#
# This file provides `add_fusilli_plugin_test()` to wrap up boilerplate for test
# setup + test installation + test script generation.
#
#===------------------------------------------------------------------------===#

include(GoogleTest)

# Set up the installed CTestTestfile.cmake. We add a ".install" postfix to
# differentiate from CTestTestfile.cmake generated by GTest during the build.
# Note: we can't simply use the GTest version as the paths are absolute, the
# test script will need to be installed (and therefore relocated) so must use
# relative paths.
set(_FUSILLI_PLUGIN_INSTALLED_CTEST_FILE "${CMAKE_CURRENT_BINARY_DIR}/CTestTestfile.cmake.install")

file(WRITE "${_FUSILLI_PLUGIN_INSTALLED_CTEST_FILE}"
    "# Autogenerated CTestTestfile for installed fusilli-plugin tests\n"
    "# Generated by fusilli-plugin build system\n\n"
)

install(
    FILES "${_FUSILLI_PLUGIN_INSTALLED_CTEST_FILE}"
    DESTINATION "${CMAKE_INSTALL_BINDIR}/fusilli_plugin_test_infra"
    RENAME CTestTestfile.cmake
)

# Creates a fusilli plugin test.
#
# To support build configurations (such as TheRock) that require tests to run on
# a different machine from where they were built, add_fusilli_plugin_test builds
# and _installs_ tests + CTest runner scripts
#
#  add_fusilli_plugin_test(
#    NAME <test-name>
#    SRCS <file> [<file> ...]
#    DEPS <dep> [<dep> ...]
#    [COMPILE_DEFS <def> [<def> ...]]
#  )
#
# NAME
#  The name of the test executable (required).
#
# SRCS
#  Source files to compile into the executable (required).
#
# DEPS
#  Library dependencies to be linked to this target.
#
# COMPILE_DEFS
#  Compile definitions to add to the target.
function(add_fusilli_plugin_test)
  cmake_parse_arguments(
    ARG                       # prefix
    ""                        # options
    "NAME"                    # one value keywords
    "SRCS;DEPS;COMPILE_DEFS"  # multi-value keywords
    ${ARGN}                   # extra arguments
  )

  if(NOT DEFINED ARG_NAME)
    message(FATAL_ERROR "add_fusilli_plugin_test: NAME is required")
  endif()

  if(NOT DEFINED ARG_SRCS)
    message(FATAL_ERROR "add_fusilli_plugin_test: SRCS is required")
  endif()

  # Create executable
  add_executable(${ARG_NAME} ${ARG_SRCS})

  # When installed all tests are at the same level in the file hierarchy, ensure
  # tests are at the same level in the build directory as well
  set_target_properties(${ARG_NAME} PROPERTIES
      RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/test"
  )

  # Set compile options
  target_compile_options(${ARG_NAME} PRIVATE ${FUSILLI_PLUGIN_WARNING_COMPILE_OPTIONS})

  # Link dependencies
  target_link_libraries(${ARG_NAME} PRIVATE ${ARG_DEPS})

  # Add compile definitions if provided
  if(DEFINED ARG_COMPILE_DEFS)
    target_compile_definitions(${ARG_NAME} PRIVATE ${ARG_COMPILE_DEFS})
  endif()

  # Register with CTest
  gtest_discover_tests(${ARG_NAME}
      PROPERTIES
        ENVIRONMENT "HIPDNN_LOG_LEVEL=info"
        ENVIRONMENT "FUSILLI_LOG_INFO=1"
        ENVIRONMENT "FUSILLI_LOG_FILE=stdout"
  )

  # Install test executable
  install(TARGETS ${ARG_NAME}
      RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
  )

  # Append to installed CTestTestfile.cmake. Paths are relative as tests get
  # relocated on install.
  file(APPEND "${_FUSILLI_PLUGIN_INSTALLED_CTEST_FILE}"
      "add_test(${ARG_NAME} \"../${ARG_NAME}\")\n"
  )
  file(APPEND "${_FUSILLI_PLUGIN_INSTALLED_CTEST_FILE}"
      "\n# Set environment variables\n"
      "set_tests_properties(${ARG_NAME}\n"
      "    PROPERTIES\n"
      "      ENVIRONMENT \"HIPDNN_LOG_LEVEL=info\"\n"
      "      ENVIRONMENT \"FUSILLI_LOG_INFO=1\"\n"
      "      ENVIRONMENT \"FUSILLI_LOG_FILE=stdout\"\n"
      ")\n"
  )
endfunction()
